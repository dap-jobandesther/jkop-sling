
/*
 * This file is part of Jkop
 * Copyright (c) 2016 Job and Esther Technologies, Inc.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

class is StringObject:

func forNow() static as this
{
	return(forTimeSeconds(SystemClock.asSeconds()))
}

func forTimeSeconds(seconds as long) static as this
{
	var v = new this()
	if(v == null) {
		return(null)
	}
	v.setTimeSeconds(seconds)
	return(v)
}

func forTimeValue(tv as TimeValue) static as this
{
	if(tv == null) {
		return(null)
	}
	return(forTimeSeconds(tv.getSeconds()))
}

var timeSeconds as long
prop weekDay as int
prop dayOfMonth as int
prop month as int
prop year as int
prop hours as int
prop minutes as int
prop seconds as int

func onTimeSecondsChanged(seconds as long #used)
{
	IFDEF("target_cs") {
		lang "cs" {{{
			var dt = new System.DateTime(1970, 1, 1).AddSeconds(seconds);
			setWeekDay((int)dt.DayOfWeek);
			setDayOfMonth(dt.Day);
			setMonth(dt.Month);
			setYear(dt.Year);
			setHours(dt.Hour);
			setMinutes(dt.Minute);
			setSeconds(dt.Second);
		}}}
	}
	ELSE IFDEF("target_java") {
		/* FIXME
			embed "java" {{{
				java.util.TimeZone tz = java.util.TimeZone.getDefault();
				if(utc == true) {
					tz = java.util.TimeZone.getTimeZone("UTC");
				}
				java.util.Calendar cal = java.util.Calendar.getInstance();
				cal.setTimeZone(tz);
				cal.setTimeInMillis(seconds*1000);
				dday = cal.get(java.util.Calendar.DAY_OF_MONTH);
				dmonth = cal.get(java.util.Calendar.MONTH) + 1;
				dyear = cal.get(java.util.Calendar.YEAR);
				dwday = cal.get(java.util.Calendar.DAY_OF_WEEK);
				dhours = cal.get(java.util.Calendar.HOUR_OF_DAY);
				dmins = cal.get(java.util.Calendar.MINUTE);
				dseconds = cal.get(java.util.Calendar.SECOND);
			}}}
		*/
		ERROR("Not implemented.")
	}
	ELSE IFDEF("target_js") {
		lang "js" {{{
			var date = new Date(seconds * 1000);
			this.year = date.getFullYear();
			this.month = date.getMonth() + 1;
			this.dayOfMonth = date.getDate();
			this.weekDay = date.getDay() + 1;
			this.hours = date.getHours();
			this.minutes = date.getMinutes();
			this.seconds = date.getSeconds();
		}}}
	}
	ELSE IFDEF("target_c") {
		/* FIXME
		if(utc == false) {
			embed {{{
				localtime_r(&tp, &lt);
			}}}
		}
		else {
			embed {{{
				gmtime_r(&tp, &lt);
			}}}
		}
		int dwday, dday, dmonth, dyear, dhours, dmins, dseconds;
		embed {{{
			dwday = lt.tm_wday + 1;
			dday = lt.tm_mday;
			dmonth = lt.tm_mon + 1;
			dyear = 1900 + lt.tm_year;
			dhours = lt.tm_hour;
			dmins = lt.tm_min;
			dseconds = lt.tm_sec;
		}}}
		*/
		ERROR("Not implemented.")
	}
	ELSE {
		ERROR("Not implemented.")
	}
}

func getTimeSeconds as long
{
	return(timeSeconds)
}

func setTimeSeconds(seconds as long)
{
	timeSeconds = seconds
	onTimeSecondsChanged(seconds)
}

func toStringDate(delim as char = '-') as string
{
	var sb = new StringBuilder()
	sb.append(String.forIntegerWithPadding(getYear(), 4))
	if(delim > 0) {
		sb.append(delim)
	}
	sb.append(String.forIntegerWithPadding(getMonth(), 2))
	if(delim > 0) {
		sb.append(delim)
	}
	sb.append(String.forIntegerWithPadding(getDayOfMonth(), 2))
	return(sb.toString())
}

func toStringTime(delim as char = '-') as string
{
	var sb = new StringBuilder()
	sb.append(String.forIntegerWithPadding(getHours(), 2))
	if(delim > 0) {
		sb.append(delim)
	}
	sb.append(String.forIntegerWithPadding(getMinutes(), 2))
	if(delim > 0) {
		sb.append(delim)
	}
	sb.append(String.forIntegerWithPadding(getSeconds(), 2))
	return(sb.toString())
}

func toStringDateTime as string
{
	var sb = new StringBuilder()
	sb.append(toStringDate())
	sb.append(" ")
	sb.append(toStringTime())
	return(sb.toString())
}

func toString as string
{
	return(toStringDateTime())
}
